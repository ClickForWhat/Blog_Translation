要在汇编中调用函数，需要[跳转](https://en.algorithmica.org/hpc/architecture/loops)到它的开头，然后再跳转回来。但是两个重要的问题出现了：

1. 如果调用者和被调用者需要使用相同的寄存器怎么办？
2. 我们如何”回到“之前的函数？

这两个问题都可以通过在内存中开辟一个专用的位置来解决，我们可以在调用函数之前写入从函数返回的所有信息。这个位置称为*堆栈*。

# 堆栈 - *The Stack*

硬件堆栈的工作方式与软件堆栈相同，并且类似地实现为两个指针：

+ 基指针标志着堆栈的开始，通常存储在 `rbp` 中。
+ 栈指针标志着堆栈的最后一个元素，通常存储在 `rsp` 中。

当你需要调用一个函数时，将所有的局部变量（你也可以在其他情况下这样做，例如，当你用完寄存器的时候）和当前的指令指针压到堆栈上，然后跳转到函数的开头。从函数中退出时，查看存储在栈顶的指针，跳转到那里，然后小心地将存储在栈上的所有变量读回它们的寄存器。

你可以通过普通的内存操作和跳转来实现这些操作，但是由于使用的频率相当高，有 4 个特殊的指令来更直接地完成这些操作：

+ `push`，在堆栈指针处写入数据并对其递减。
+ `pop`，从堆栈指针读取数据并对其递增。
+ `call`，将接下来的指令的地址放在堆栈顶部，并跳转到一个标签。
+ `ret`，从堆栈顶部读取地址并跳转到它。

如果它们在硬件上没有实际的指令的话，你把它们叫做”语法糖“也没什么问题——毕竟它只是融合了两个指令：

```asm
; "push rax"
sub rsp, 8
mov QWORD PTR[rsp], rax

; "pop rax"
mov rax, QWORD PTR[rsp]
add rsp, 8

; "call func"
push rip ; <- 指令指针(尽管像那样访问它可能是非法的)
jmp func

; "ret"
pop  rcx ; <- 选择任何未使用的寄存器
jmp rcx
```

在 `rbp` 和 `rsp` 两者所指向地址之间的区域被称为”栈帧“，这也是通常存储函数的局部变量的地方。它是在程序开始时预先分配的，如果在堆栈上保存的数据超过其容量（Linux上默认为 8MB），就会遇到栈溢出错误。由于现代操作系统在实际读取或写入它们的地址空间之前不会分配内存页，因此你可以自由地指定一个非常大的栈大小。请注意，这相当于你可以使用的栈内存的最大大小，而不是你必须使用这么大的空间。

# 调用约定 - *Calling Conventions*

开发编译器和操作系统的人提出了如何编写和调用函数的[约定](https://wiki.osdev.org/Calling_Conventions)。这些约定实现了一些重要的[软件工程奇迹](https://en.algorithmica.org/hpc/compilation/stages/)，例如将编译分解为单独的单元，重用已经编译的库，甚至用不同的编程语言编写它们。

考虑如下 C 语言的例子：

```C
int square(int x) {
    return x * x;
}

int distance(int x, int y) {
    return square(x) + square(y);
}
```

根据约定，函数应该从`rdi`、`rsi`、`rdx`、`rcx`、`r8`、`r9`中获取参数（如果还不够的话，还可以从堆栈里拿），将返回值放入 `rax` 中，然后返回。因此，`square` 作为一个简单的单参数函数，可以这样实现：

```asm
square:             ; x = edi, ret = eax
    imul edi, edi
    mov  eax, edi
    ret
```

每次我们从 `distance` 调用它，我们只需要花点时间来保持它的局部变量；

```asm
distance:           ; x = rdi/edi, y = rsi/esi, ret = rax/eax
    push rdi
    push rsi
    call square     ; eax = square(x)
    pop  rsi
    pop  rdi
	
    mov  ebx, eax   ; save result of x^2
    mov  rdi, rsi   ; move new x=y
	
    push rdi
    push rsi
    call square     ; eax = square(x=y)
    pop  rsi
    pop  rdi
	
    add  eax, ebx   ; x^2 + y^2
    ret
```

实际上还有更多的细微差别，但我们不会在这里详细讨论，因为本书是关于性能的，处理函数调用的最佳方法实际上是首先避免进行函数调用。

# 内联 - *Inlining*

在堆栈之间来回移动数据会给像这样的小函数带来明显的开销。而你必须这样做的原因是，一般来说，你不知道被调用者是否在修改你用于存储局部变量的寄存器。但是当你知道 `square` 的代码会做什么时，你可以通过将数据存储在 `square` 明确不会修改的寄存器中来解决这个问题。

```asm
distance:
    call square
    mov  ebx, eax
    mov  edi, esi
    call square
    add  eax, ebx
    ret
```

这样更好，但是我们仍然隐式地访问堆栈内存：你仍需要为每个函数 `push` 和 `pop` 指令指针。在这种的简单情况下，我们可以通过将被调用者的代码拼接到调用者的代码中并解决寄存器上的冲突来内联函数调用。以我们的例子为例：

```asm
distance:
    imul edi, edi       ; edi = x^2
    imul esi, esi       ; esi = y^2
    add  edi, esi
    mov  eax, edi       ; 不存在“add eax,edi,esi”这样的东西，所以需要一个单独的mov
    ret
```

这与编译器从这个代码片段中产生的优化结果非常接近，只是它们使用 [`lea` 技巧](https://en.algorithmica.org/hpc/architecture/assembly)使生成的机器码少了几个字节：

```asm
distance:
    imul edi, edi       ; edi = x^2
    imul esi, esi       ; esi = y^2
    lea  eax, [rdi+rsi] ; eax = x^2 + y^2
    ret
```

在这种情况下，函数内联显然是有益的，编译器大多会[自动](https://en.algorithmica.org/hpc/compilation/situational)完成它，但也有不这样做的情况，我们将[稍微讨论一下](https://en.algorithmica.org/hpc/architecture/layout)。

# 尾调用消除 - *Tail Call Elimination*

当被调用者不再进行任何其他函数调用时，或者至少在这些调用不是递归的情况下，内联是很简单的。让我们来看一个更复杂的例子。考虑一下阶乘的递归计算：

```C
int factorial(int n) {
    if (n == 0)
        return 1;
    return factorial(n - 1) * n;
}
```

等价的汇编：

```asm
; n = edi, ret = eax
factorial:
    test edi, edi   ; test if a value is zero
    jne  nonzero    ; (the machine code of "cmp rax, 0" would be one byte longer)
    mov  eax, 1     ; return 1
    ret
nonzero:
    push edi        ; save n to use later in multiplication
    sub  edi, 1
    call factorial  ; call f(n - 1)
    pop  edi
    imul eax, edi
    ret
```

即使函数是递归的，仍然有可能重构它来减少调用开销。这里是函数尾部递归的情况，也就是说，它在进行递归调用后立即返回。由于调用后不需要任何操作，因此也不需要在堆栈上存储任何内容，并且可以安全地将递归调用替换为跳到开始处，从而有效地将函数转换为循环。

为了使我们的 `factorial` 函数尾部递归，我们可以向它传递一个”当前 product“ 参数：

```C
int factorial(int n, int p = 1) {
    if (n == 0)
        return p;
    return factorial(n - 1, p * n);
}
```

然后这个函数可以很容易地折叠成一个循环：

```asm
; assuming n > 0
factorial:
    mov  eax, 1
loop:
    imul eax, edi
    sub  edi, 1
    jne  loop
    ret
```

递归很慢的主要原因是它需要向堆栈读取和写入数据，而迭代和尾递归算法则不需要。这个概念在函数式编程中非常重要，因为函数式编程中没有循环，你只能使用函数。如果没有尾调用消除，函数式程序将需要更多的时间和内存来执行。